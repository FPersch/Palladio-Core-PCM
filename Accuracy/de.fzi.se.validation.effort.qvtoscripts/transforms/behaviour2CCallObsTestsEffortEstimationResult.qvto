import de.fzi.se.validation.effort.MathFunctionsLibrary;
import ProbabilityLib;

modeltype PCM_SEFF uses 'http://sdq.ipd.uka.de/PalladioComponentModel/SEFF/4.0';
modeltype EER uses 'http://se.fzi.de/ValidationEffortEstimation/0.5';

transformation behaviour2CCallObsTestsEffortEstimation(in inbehaviour : PCM_SEFF, 
									      			  out outdesc : EER) 
access library ProbabilityLib();

main() {
	log('RD-SEFF to EffortEstimationResult (CCallObsTests) transformation started.');
	inbehaviour.objects()[EffortEstimationResult]->map createEffortEstimation(inbehaviour);
	log('RD-SEFF to EffortEstimationResult (CCallObsTests) transformation finished.');
}

mapping inout EffortEstimationResult::createEffortEstimation(in behaviour : PCM_SEFF) {
	log('Estimate started for targetId=' + self.targetId + ', alpha=' + self.alpha.toString());
	alpha := self.alpha;
	targetId := self.targetId;
	resourceDemandingBehaviour := behaviour.objects()[ResourceDemandingBehaviour]->selectOne(b : ResourceDemandingBehaviour | b.id = targetId);
	numberTestcases := resourceDemandingBehaviour.CCallObsTests(alpha);
	log('Estimate complete. numberTestcases=' + self.numberTestcases.toString());
}

query ResourceDemandingBehaviour::CCallObsTests(in confidence : Real) : Integer {
	var required : Integer = 0;
	self.steps_Behaviour->select(a | a.oclIsKindOf(AbstractAction))->forEach(action) {
		required := required + action.getEffort(confidence);
		if(action.containsCalls()) then {
			var calls : Calls; // TODO
			calls->forEach(c) {
				var probFreqGreaterZero : Real = 1 - c.frequency.expression.ProbabilityZero();
				required := required + MinRequiredTests(probFreqGreaterZero, confidence);
			};
		} endif;
	};
	return required;
}

helper AbstractAction::getEffort(in confidence : Real) : Integer {
	return 0;
}

helper BranchAction::getEffort(in confidence : Real) : Integer {
	var r : Integer; // result
	self.branches_Branch->forEach(branch) {
		if (branch.oclIsKindOf(GuardedBranchTransition)) then {
			r := branch.branchBehaviour_BranchTransition.CCallObsTests(confidence);
		} else { // ProbabilisticBranchCondition
			var probBranch : ProbabilisticBranchTransition = branch.oclAsType(ProbabilisticBranchTransition);
			r := branch.branchBehaviour_BranchTransition.CCallObsTests(confidence);
			r := r * MinRequiredTests(probBranch.branchProbability, confidence);
		} endif;
	};
	return r;
}

helper LoopAction::getEffort(in confidence : Real, in n : Integer) : Integer {
	var r : Integer = self.bodyBehaviour_Loop.CCallObsTests(confidence);
	var probFreqGreaterZero : Real = 1 - self.iterationCount_LoopAction.expression.ProbabilityZero();
	r := r * MinRequiredTests(probFreqGreaterZero, confidence);
	return r;
}

helper ForkAction::getEffort(in confidence : Real, in n : Integer) : Integer {
	var r : Integer = 0;
	self.asynchronousForkedBehaviours_ForkAction->forEach(fb) {
		r = r + fb.CCallObsTests(confidence);
	};
	self.synchronisingBehaviours_ForkAction->forEach(fb) {
	//	r := r + fb.CBranchTests(confidence);
		assert(false) with log('ERROR: synchronsing behaviours_forkaction not implemented.');
	};
	return r;
}
