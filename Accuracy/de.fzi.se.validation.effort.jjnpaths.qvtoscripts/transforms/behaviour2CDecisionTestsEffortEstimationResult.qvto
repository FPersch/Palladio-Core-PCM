import de.fzi.se.validation.effort.MathFunctionsLibrary;
import ProbabilityLib;

modeltype PCM_SEFF uses 'http://sdq.ipd.uka.de/PalladioComponentModel/SEFF/4.0';
modeltype EER uses 'http://se.fzi.de/ValidationEffortEstimation/0.5';

transformation behaviour2CDecisionTestsEffortEstimation(in inbehaviour : PCM_SEFF, 
									      			  out outdesc : EER) 
access library ProbabilityLib();

main() {
	log('RD-SEFF to EffortEstimationResult (CDecisionTests) transformation started.');
	inbehaviour.objects()[ResourceDemandingBehaviour]->map createEffortEstimation();
	log('RD-SEFF to EffortEstimationResult (CDecisionTests) transformation finished.');
}

mapping ResourceDemandingBehaviour::createEffortEstimation() : EffortEstimationResult {
	log('Processing ' + self.metaClassName() + ' ' + self.id + ' ...');
	alpha := 0.5; // TODO: confidence?
	numberTestcases := self.CDecisionTests(alpha);
	resourceDemandingBehaviour := self;
}


query ResourceDemandingBehaviour::CDecisionTests(in confidence : Real) : Integer {
	var required : Integer = 0;
	self.steps_Behaviour->select(a | a.oclIsKindOf(AbstractAction))->forEach(action) {
		required := required + action.getEffort(confidence);
	};
	return required;
}

helper AbstractAction::getEffort(in confidence : Real) : Integer {
	return 1;
}

helper BranchAction::getEffort(in confidence : Real) : Integer {
	var r : Integer; // result
	self.branches_Branch->forEach(branch) {
		if (branch.oclIsKindOf(GuardedBranchTransition)) then {
			r := 1.max(branch.branchBehaviour_BranchTransition.CDecisionTests(confidence));
		} else { // ProbabilisticBranchCondition
			var probBranch : ProbabilisticBranchTransition = branch.oclAsType(ProbabilisticBranchTransition);
			r := 1.max(branch.branchBehaviour_BranchTransition.CDecisionTests(confidence));
			r := r * MinRequiredTests(probBranch.branchProbability, confidence);
		} endif;
	};
	return r;
}

helper LoopAction::getEffort(in confidence : Real, in n : Integer) : Integer {
	var r : Integer = self.bodyBehaviour_Loop.CDecisionTests(confidence);
	return r;
}

helper ForkAction::getEffort(in confidence : Real, in n : Integer) : Integer {
	var r : Integer = 0;
	self.asynchronousForkedBehaviours_ForkAction->forEach(fb) {
		r = r + fb.CDecisionTests(confidence);
	};
	return r;
}
